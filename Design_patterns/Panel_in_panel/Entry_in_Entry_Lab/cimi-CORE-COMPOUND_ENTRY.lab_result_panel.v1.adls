archetype (adl_version=1.5.1)
	CIMI-Core-COMPOUND_ENTRY.lab_result_panel.v1.0.0

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["name"] = <"Thomas Beale">
		["organisation"] = <"CIMI">
		["email"] = <"thomas.beale@oceaninformatics.com">
		["date"] = <"10/02/2014">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"CIMI Compund Entry that has slots for 'atomic' Entries">
			copyright = <"Â© 2014 CIMI">
		>
	>
	lifecycle_state = <"unmanaged">

definition
	COMPOUND_ENTRY[id1] matches {	-- Multi-level panel
		participation matches {
			PARTICIPATION[id12] matches {	-- subject
				function matches {[at3]}
			}
		}
		data matches {
			ELEMENT[id2] matches {	-- Requester order id
				value matches {
					TEXT[id3] 
				}
			}
			ELEMENT[id4] matches {	-- Panel status
				value matches {[ac1]}		-- Panel statuses
			}
			ELEMENT[id6] matches {	-- Abnormal interpretation
				value matches {
					TEXT[id7] 
				}
			}
		}
		parts matches {
			allow_archetype INDIVISIBLE_ENTRY[id8] occurrences matches {0..*} matches {	-- Level 1 Panel item
				include
					archetype_id/value matches {/CIMI-Core-INDIVISIBLE_ENTRY\..*\.v1.*/}
			}
			COMPOUND_ENTRY[id9] matches {	-- Level 2 Panel items
				parts matches {
					allow_archetype INDIVISIBLE_ENTRY[id10] occurrences matches {0..*} matches {	-- Level 2 Panel item
						include
							archetype_id/value matches {/CIMI-Core-INDIVISIBLE_ENTRY\..*\.v1.*/}
					}
				}
			}
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1"] = <
				text = <"Multi-level panel">
				description = <"Multi-level panel">
			>
			["id2"] = <
				text = <"Requester order id">
				description = <"Requester order id">
			>
			["id4"] = <
				text = <"Panel status">
				description = <"Panel status">
			>
			["id6"] = <
				text = <"Abnormal interpretation">
				description = <"Abnormal interpretation">
			>
			["id8"] = <
				text = <"Level 1 Panel item">
				description = <"Level 1 Panel item">
			>
			["id9"] = <
				text = <"Level 2 Panel items">
				description = <"Level 2 Panel items">
			>
			["id10"] = <
				text = <"Level 2 Panel item">
				description = <"Level 2 Panel item">
			>
			["id12"] = <
				text = <"subject">
				description = <"subject participation">
			>
			["ac1"] = <
				text = <"Panel statuses">
				description = <"Panel statuses">
			>
			["at1"] = <
				text = <"Complete">
				description = <"Panel complete">
			>
			["at2"] = <
				text = <"Interim">
				description = <"Panel in interim state">
			>
			["at3"] = <
				text = <"subject">
				description = <"subject of recording">
			>
		>
	>
	term_bindings = <
		["snomedct"] = <
			["id1"] = <http://snomedct.info/id/413139004>
		>
	>
	value_sets = <
		["ac1"] = <
			id = <"ac1">
			members = <"at1", "at2">
		>
	>
