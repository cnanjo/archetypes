archetype (adl_version=2.0.6; rm_release=2.0.2)
  CIMI-CORE-ITEM_GROUP.proposal.v1.0.0

specialize
  CIMI-CORE-ITEM_GROUP.action_context.v1

language
  original_language = <[ISO_639-1::en]>

description
  original_author = <
    ["name"] = <"Claude Nanjo">
    ["organisation"] = <"Cognitive Medical Systems">
    ["email"] = <"cnanjo@gmail.com">
    ["date"] = <"06/15/2016">
  >
  details = <
    ["en"] = <
      language = <[ISO_639-1::en]>
      purpose = <"Top-level reference archetype for CIMI 'clinical statement' concept">
      use = <"Use as a specialisation precursor for other high-level reference archetypes.">
      keywords = <"statement","modality">
    >
  >
  other_details = <["model_level"] = <"reference">>
  lifecycle_state = <"unmanaged">
  other_contributors = <"Linda Bird <lbi@ihtsdo.org>","Joey Coyle <joey@xcoyle.com>","Stan Huff <Stan.Huff@imail.org>","Harold Solbrig <Solbrig.Harold@mayo.edu>","Thomas Beale <thomas.beale@oceaninformatics.com>">
  custodian_namespace = <"org.opencimi">
  custodian_organisation = <"Clinical Information Modeling Initiative <http://opencimi.org>">
  licence = <"Creative Commons CC-BY <https://creativecommons.org/licenses/by/3.0/>">
  copyright = <"Copyright (c) Clinical Information Modelling Initiative <http://opencimi.org>">
  ip_acknowledgements = <
    ["loinc"] = <"This content from LOINC® is copyright © 1995 Regenstrief Institute, Inc. and the LOINC Committee, and available at no cost under the license at http://loinc.org/terms-of-use">
    ["snomed"] = <"This specification includes SNOMED Clinical Terms® (SNOMED CT®) which is used by permission of the International Health Terminology Standards Development Organization (IHTSDO). All rights reserved. SNOMED CT® was originally created by the College of American Pathologists. 'SNOMED' and 'SNOMED CT' are registered trademarks of the IHTSDO (www.ihtsdo.org). Use of any SNOMED CT content (including codes, identifiers, terms, expressions or derivatives) in any operational system (including any Data Creation System or Data Analysis System as defined in the SNOMED CT Affiliate License Agreement) is subject to the terms and conditions of the SNOMED CT Affiliate License (www.ihtsdo.org/license.pdf) or to a written variation to that agreement that has been agreed and formally executed between the IHTSDO and the Affiliate Licensee.">
  >

definition
  ITEM_GROUP[id1.1.1.1] matches { -- Proposal
    item matches {
      ELEMENT[id0.1.1.1] matches { -- Action context
        value matches {[ac0.1.1.1]} -- Requested (Note how requested in a proposal context means a proposal as opposed to an order)
      }
      use_archetype ITEM_GROUP[id0.0.0.5, CIMI-CORE-ITEM_GROUP.request_action.v1] occurrences matches {0..1}
      ELEMENT[id0.0.0.10] occurrences matches {0..1} matches { -- Urgency
        value matches {
          CODED_TEXT[id0.0.0.205]
        }
      }
      ELEMENT[id0.0.0.15] occurrences matches {0..1} matches { -- Is PRN
        value matches {
          YESNO[id0.0.210]
        }
      }
      use_archetype ITEM_GROUP[id0.0.0.20, CIMI-CORE-ITEM_GROUP.justification.v1] occurrences matches {0..*} -- PRN reason
      ELEMENT[id0.0.0.25] occurrences matches {0..1} matches { -- Expected performance time
        value matches {
          INTERVAL_VALUE<DATE_TIME>[id0.0.0.105]
        }
      }
      ELEMENT[id0.0.0.30] occurrences matches {0..1} -- Placeholder for timing (over fixed calendar times)
    }
    participation matches {
      PARTICIPATION[id0.0.0.100] matches { -- Expected performer
        party matches { -- use_archetype ACTOR[id0.0.0.300, CIMI-CORE-ACTOR.actor.v1]
          ACTOR[id0.0.0.300]
        }
      }
    }
  }

terminology
  term_definitions = <
    ["en"] = <
      ["id1.1.1.1"] = <
        text = <"Proposal">
        description = <"An offer or a suggestion to perform a healthcare act. A recommendation to a provider is an example of proposal made by a CDS system. A proposal must be accepted by an entity in order for it to be performed.">
      >
      ["id0.1.1.1"] = <
        text = <"Action context">
        description = <"Context type indicating that context is for an action such as a procedure.">
      >
      ["id0.0.0.5"] = <
        text = <"Proposed">
        description = <"Provenance information about who proposed the action, when, where, why, how, etc...">
      >
      ["id0.0.0.10"] = <
        text = <"Urgency">
        description = <"Characterizes how quickly the proposed act must be initiated. Includes concepts such as stat, urgent, routine.">
      >
      ["id0.0.0.15"] = <
        text = <"Is PRN">
        description = <"Indicates whether the act being proposed should be performed only if needed, i.e., when a certain condition occurs. The condition can be described in the attribute prnReason.">
      >
      ["id0.0.0.20"] = <
        text = <"PRN reason">
        description = <"The proposed act must be performed if the indicated conditions occur, e.g.., shortness of breath, SpO2 less than x%, insomnia, nausea.">
      >
      ["id0.0.0.25"] = <
        text = <"Expected performance time">
        description = <"The time when the action is expected to be performed.">
      >
      ["id0.0.0.30"] = <
        text = <"Schedule">
        description = <"If the procedure is repeated, the frequency pattern for repetitions.">
      >
      ["id0.0.0.100"] = <
        text = <"Expected performer">
        description = <"Persons who are expected to or intended to perform this action, e.g., the person who will perform the surgery.">
      >
      ["at0.0.0.1"] = <
        text = <"Under consideration">
        description = <"Code indicating that the activity is proposed (an under consideration). Perhaps we need a synonym for 'Proposed'. Could be an extension as a subtype of 'Under consideration'.">
      >
      ["ac0.1.1.1"] = <
        text = <"Requested">
        description = <"Value set containing a single code indicating that the context is 'Under consideration' (SCTID: 385642001).">
      >
    >
  >

  term_bindings = <
    ["snomed-ct"] = <
      items = <
        ["at0.0.0.1"] = <http://snomedct.info/id/385642001> -- Under consideration
      >
    >
  >

